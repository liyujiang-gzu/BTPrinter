import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

android {
    defaultConfig {
        applicationId androidAppPackageName
        versionCode androidAppVersionCode as int
        versionName androidAppVersionName
        //必须添加，不添加编译不通过，后面的数值，目前好像任何字符串都可以
        flavorDimensions "channel"
        ndk {
            //支持的CPU架构：armeabi、armeabi-v7a、arm64-v8a、x86、x86_64、mips、mips64
            //目前主流手机都支持armeabi和armeabi-v7a，电脑上的模拟器支持x86，mips基本不用于手机
            if (androidAppX86Support.toString().equalsIgnoreCase("true")) {
                abiFilters 'armeabi-v7a', 'x86'
            } else {
                abiFilters 'armeabi-v7a'
            }
        }
        signingConfigs {
            //直接使用真实的密钥库文件，避免微信分享等SDK校验失败问题
            releaseConfig {
                storeFile project.getParent().file(signingStorePath)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
        manifestPlaceholders = [
                LI_PACKAGE: androidAppPackageName,
                LI_CHANNEL: "developer-default",
                LI_SCHEME : androidAppScheme,
        ]
    }
    // 打包渠道列表，会按渠道名字母顺序执行
    productFlavors {
        adeveloper {
            dimension "channel"
        }
        if (androidAppMultiChannel.toString().equalsIgnoreCase("true")) {
            official {
                dimension "channel"
            }
            myapp {
                dimension "channel"
            }
            zhushou360 {
                dimension "channel"
            }
            baidu {
                dimension "channel"
            }
            hicloud {
                dimension "channel"
            }
            xiaomi {
                dimension "channel"
            }
            wandoujia {
                dimension "channel"
            }
        }
    }
    // 批量处理渠道，参阅 https://community.jiguang.cn/t/topic/24648
    productFlavors.all { flavor ->
        String channelName = name
        if (channelName == "adeveloper") {
            channelName = "developer-debug"
        }
        //替换AndroidManifest.xml中的默认值
        flavor.manifestPlaceholders = [
                LI_CHANNEL: channelName,
        ]
    }
    //定制渠道包文件名，参阅 https://www.jianshu.com/p/d4e8b5062da6
    applicationVariants.all { variant ->
        variant.outputs.all {
            String buildTypeName = variant.buildType.name
            if (outputFileName != null && outputFileName.endsWith(".apk")) {
                if (buildTypeName == "debug" && outputFileName.contains("developer")) {
                    println "调试模式的开发者渠道包不改名，以便使用`gradlew installAdeveloperDebug`"
                    return
                }
                File targetDir = new File(projectDir.getParent(), buildTypeName)
                targetDir.mkdirs()
                variant.getPackageApplicationProvider().get().outputDirectory = targetDir
                String fileName
                if (outputFileName.contains("developer")) {
                    fileName = "app-${buildTypeName}.apk"
                } else {
                    String flavorName = variant.productFlavors[0].name
                    String versionName = defaultConfig.versionName
                    String buildDate = new SimpleDateFormat("yyyyMMdd").format(Calendar.getInstance(Locale.CHINA).getTime())
                    fileName = "app-${buildTypeName}_${flavorName}_${versionName}_build${buildDate}.apk"
                }
                //这里只能用相对路径，不然报错
                File targetFile = new File(fileName)
                outputFileName = targetFile
                println "outputDirectory=" + targetDir.getAbsolutePath() + ", outputFileName=" + targetFile.getAbsolutePath()
            }
        }
    }
    buildTypes {
        //线上版本
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            shrinkResources false
            debuggable false
            jniDebuggable false
            zipAlignEnabled true
            signingConfig signingConfigs.releaseConfig
            buildConfigField "boolean", "PUBLISH", "Boolean.parseBoolean(\"${androidAppPublishMode}\")"
            buildConfigField "String", "SCHEME", "\"${androidAppScheme}\""
        }
        //调试版本
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            jniDebuggable true
            zipAlignEnabled true
            signingConfig signingConfigs.releaseConfig
            buildConfigField "boolean", "PUBLISH", "Boolean.parseBoolean(\"${androidAppPublishMode}\")"
            buildConfigField "String", "SCHEME", "\"${androidAppScheme}\""
        }
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:$androidxCoreVersion"
    implementation 'com.github.promeg:tinypinyin:1.0.0'
    implementation "com.github.gzuliyujiang:Scaffold:2019.10.27.0205"
}
